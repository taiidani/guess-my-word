variable "tag" {
  type    = string
  default = "latest"
}

job "guess-my-word" {
  datacenters = ["dc1"]
  type        = "service"

  update {
    min_healthy_time  = "30s"
    healthy_deadline  = "1m"
    progress_deadline = "10m"
    auto_revert       = false
  }

  group "app" {
    task "proxy" {
      driver = "docker"

      config {
        image = "traefik:v2.5"
        ports = ["http", "ui"]

        mount {
          type   = "bind"
          source = "local"
          target = "/etc/traefik"
        }
      }

      template {
        data        = <<EOF
ping: {}

accessLog: {}

entrypoints:
  web:
    address: ":81"

api:
  dashboard: true
  insecure: true

providers:
  file:
    filename: /etc/traefik/config.yaml
EOF
        destination = "local/traefik.yaml"
      }

      template {
        data        = <<EOF
http:
  routers:
    web:
      rule: "Host(`guessmyword.xyz`, `www.guessmyword.xyz`)"
      service: web
      middlewares:
        - "web-redirectregex"
    api:
      rule: "Host(`guessmyword.xyz`, `www.guessmyword.xyz`) && PathPrefix(`/api`)"
      service: api
      middlewares:
        - "web-redirectregex"

  services:
    web:
      loadBalancer:
        servers:
          - url: "http://127.0.0.1:80/"
    api:
      loadBalancer:
        servers:
          - url: "http://127.0.0.1:3000/"

  middlewares:
    web-redirectregex:
      redirectRegex:
        regex: "^http://www.guessmyword.xyz/(.*)"
        replacement: "http://guessmyword.xyz/$${1}"
        permanent: true
EOF
        destination = "local/config.yaml"
      }

      resources {
        cpu    = 100
        memory = 64
      }
    }

    task "web" {
      driver = "docker"

      config {
        image = "ghcr.io/taiidani/guess-my-word:${var.tag}"
        ports = ["web"]
      }
    }

    task "api" {
      driver = "docker"

      config {
        image = "ghcr.io/taiidani/guess-my-word:${var.tag}"
        args  = ["/app"]
        ports = ["api"]
      }

      env {
        ADDR       = "0.0.0.0"
        GIN_MODE   = "release"
        REDIS_ADDR = "127.0.0.1:6379"
        ORIGIN     = "guessmyword.xyz"
      }
    }

    volume "hashistack" {
      type      = "host"
      source    = "hashistack"
      read_only = "false"
    }

    task "redis" {
      driver = "docker"

      config {
        image = "redis:6"
        args  = ["redis-server", "--dir", "/data/guess"]
        ports = ["redis"]
      }

      volume_mount {
        volume      = "hashistack"
        destination = "/data"
        read_only   = "false"
      }
    }

    network {
      mode = "bridge"
      port "http" { static = 81 }
      port "ui" { static = 8080 }

      port "web" { to = 80 }
      port "api" { to = 3000 }
      port "redis" { static = 6379 }
    }
  }
}
